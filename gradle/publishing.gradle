buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "RADAR-CNS Distributions"
        }
    }
    developers {
        developer {
            id "radar-cns"
            name "Chris Greenhalgh"
            email "chris.greenhalgh@nottingham.ac.uk"
        }
    }
}
// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}


publishing {
    publications {
        RadarCsvFilePublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'org.radarcns'
            artifactId 'kafka-connect-csvfile-connect'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'The CSVFile connect connector is a tool for test and development streaming data between Apache Kafka and CSV Files.')
                root.appendNode('name', 'kafka-connect-csvfile-connect')
                root.appendNode('url', 'https://github.com/cgreenhalgh/CsvFile-Sink-Connector.git')
                root.children().last() + pomConfig
            }
        }
    }
}



bintray {
    user = System.getenv('BINTRAY_USER') ? System.getenv('BINTRAY_USER') : project.hasProperty('bintrayUser')
    key =  System.getenv('BINTRAY_API_KEY') ? System.getenv('BINTRAY_API_KEY') : project.hasProperty('bintrayApiKey')
    override = false
    publications = ['RadarCsvFilePublication']
    pkg {
        repo = 'org.radarcns'
        name = 'kafka-connect-csvfile-connect'
        userOrg = 'radar-cns'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/cgreenhalgh/CsvFile-Sink-Connector.git'
        version {
            name = project.version
            desc = 'Initial alpha release'
            vcsTag = System.getenv('TRAVIS_TAG')
            released  = new Date()
        }
    }
}
